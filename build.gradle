plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("io.github.goooler.shadow") version "8.1.8"
}

group = 'org.nyt'
version = '0.0.4'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven { url = 'https://maven.maxhenkel.de/repository/public' }
    maven { url = 'https://repo.subkek.space/maven-public'}
    maven { url 'https://repo.inventivetalent.org/repository/public/' }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT"
    compileOnly "de.maxhenkel.voicechat:voicechat-api:2.6.0"
    compileOnly "space.subkek:customdiscs-api:1.0.0-SNAPSHOT"
    compileOnly "net.dmulloy2:ProtocolLib:5.4.0"

    implementation "org.mineskin:java-client:3.2.1-SNAPSHOT"
    implementation "org.mineskin:java-client-jsoup:3.2.1-SNAPSHOT"
}

shadowJar {
    archiveClassifier.set('')

    relocate 'org.mineskin', 'org.nyt.simpleVoiceRadio.libs.mineskin'
    relocate 'org.jsoup', 'org.nyt.simpleVoiceRadio.libs.jsoup'

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

build {
    dependsOn shadowJar
}

tasks {
    runServer {
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

tasks.register('copyJarToServer', Copy) {
    dependsOn shadowJar
    from shadowJar.archiveFile
    into 'C:/Users/Daniil/Desktop/server/plugins'
}

//build.finalizedBy copyJarToServer